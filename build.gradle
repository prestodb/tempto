/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id "com.github.hierynomus.license" version "0.11.0"
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven-publish'

plugins.withType(JavaPlugin) {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

allprojects {
  repositories {
    mavenCentral()
  }
  apply plugin: 'license'
  license {
    header rootProject.file('apache_license_header.txt')
  }
}

ext.versions = [
        assertj      : '2.0.0',
        guava        : '18.0',
        guice        : '4.0-beta5',
        log4j        : '1.2.17',
        testng       : '6.8',
        spock        : '1.0-groovy-2.4',
        slf4j        : '1.7.5',
        hsqldb       : '2.3.2',
        dbutils      : '1.6',
        snakeyaml    : '1.15',
        cglib        : '3.1',
        httpclient   : '4.4',
        ioutils      : '2.4',
        tpchgen      : '0.4',
        jsonpath     : '2.0.0',
        commons_lang3: '3.3.2',
        commons_dbcp2: '2.1',
        hive         : '0.12.0',
        hadoop       : '0.20.2',
        presto       : '0.97',
        reflections  : '0.9.9',
        bytebuddy    : '0.5.6',
        jsch         : '0.1.52',
        mina_sshd    : '0.14.0',
        freemarker   : '2.3.22'
]

ext.libraries = [
        assertj      : "org.assertj:assertj-core:${versions.assertj}",
        guava        : "com.google.guava:guava:${versions.guava}",
        guice        : ["com.google.inject:guice:${versions.guice}",
                        "com.google.inject.extensions:guice-multibindings:${versions.guice}"],
        log4j        : "log4j:log4j:${versions.log4j}",
        testng       : "org.testng:testng:${versions.testng}",
        commons_lang3: "org.apache.commons:commons-lang3:${versions.commons_lang3}",
        commons_dbcp2: "org.apache.commons:commons-dbcp2:${versions.commons_dbcp2}",
        spock        : ["org.spockframework:spock-core:${versions.spock}",
                        "cglib:cglib:${versions.cglib}"],
        httpclient   : "org.apache.httpcomponents:httpclient:${versions.httpclient}",
        ioutils      : "commons-io:commons-io:${versions.ioutils}",
        hsqldb       : "org.hsqldb:hsqldb:${versions.hsqldb}",
        dbutils      : "commons-dbutils:commons-dbutils:${versions.dbutils}",
        slf4j_api    : "org.slf4j:slf4j-api:${versions.slf4j}",
        slf4j_log4j  : "org.slf4j:slf4j-log4j12:${versions.slf4j}",
        snakeyaml    : "org.yaml:snakeyaml:${versions.snakeyaml}",
        tpchgen      : "io.airlift.tpch:tpch:${versions.tpchgen}",
        jsonpath     : "com.jayway.jsonpath:json-path:${versions.jsonpath}",
        hive_jdbc    : "org.apache.hive:hive-jdbc:${versions.hive}",
        hadoop_core  : "org.apache.hadoop:hadoop-core:${versions.hadoop}",
        presto_jdbc  : "com.facebook.presto:presto-jdbc:${versions.presto}",
        reflections  : "org.reflections:reflections:${versions.reflections}",
        bytebuddy    : "net.bytebuddy:byte-buddy:${versions.bytebuddy}",
        jsch         : "com.jcraft:jsch:${versions.jsch}",
        mina_sshd    : "org.apache.sshd:apache-sshd:${versions.mina_sshd}",
        postgresql_jdbc    : "org.postgresql:postgresql:9.3-1102-jdbc4",
        commons_cli  : "commons-cli:commons-cli:1.2",
        freemarker   : "org.freemarker:freemarker:${versions.freemarker}"
]

ext.tempto_core = project(':tempto-core')
ext.tempto_logging_log4j = project(':tempto-logging-log4j')
ext.tempto_examples = project(':tempto-examples')
ext.expected_result_generator = project(':expected-result-generator')
ext.third_party_deps = project(':third-party-deps')
ext.tempto_version = '1.0.28-SNAPSHOT' // changed by arosa
ext.publish_maven_repository = {
  url 'http://phabricator.td.teradata.com:8081/nexus/content/repositories/test-framework'
  credentials {
    username 'automation'
    password 'hadoopdb'
  }
}


task wrapper(type: Wrapper) {
  gradleVersion = '2.3'
}

task packageAndUploadLaunchScript(type: Exec) {
  workingDir 'tempto-launch-script/'
  commandLine 'python', 'setup.py', 'bdist_wheel', 'upload', '-r', 'pypi_internal'
}

